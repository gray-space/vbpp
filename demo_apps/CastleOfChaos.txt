5  DIM N(65),S(65),E(65),W(65),IT(15),IT$(13),FLAG(10),MN$(15),MH(15),MD(15),MP(15),MR(15),MM(15),MC(15)
7  FOR X = 1 TO 65: READ N(X),S(X),E(X),W(X)
8  NEXT : FOR X = 1 TO 13: READ IT$(X),IT(X): NEXT 
9  FOR X = 1 TO 15: READ MN$(X),MH(X),MD(X),MR(X),MP(X): NEXT 
10  GOTO 60000
20  HOME : PRINT "IT IS NOW TIME FOR YOU TO BEGIN YOUR    ADVENTURE."
25 RM = 2: GOTO 3000
100  REM   F.L.I.P.              (FULL LINE INPUT PARSER)
102  FOR Z = 1 TO 15: IF MR(Z) = RM THEN  GOSUB 2300
103  NEXT 
104  IF RM > 28 AND RM < 52 AND FLAG(2) <  > 1 THEN  PRINT "YOU TRIP AND BREAK YOUR NECK": GOTO 2700
105  REM  CREATED BY GARY GRAY 
106  REM  VERSION 1.1 11/23/84 
110  IF FLAG = 1 THEN FLAG = 0: GOTO 140
120  INPUT ">";A$
121  IF A$ = "" THEN  GOTO 400
125  IF  LEN (A$) = 1 THEN V$ = A$: GOTO 400
127  IF  RIGHT$ (A$,1) = " " THEN A$ =  LEFT$ (A$, LEN (A$) - 1)
130 FLAG = 0
140 N$ = "": IF A$ <  > "" THEN DO$ = "":V$ = ""
150  FOR X = 1 TO  LEN (A$): IF  MID$ (A$,X,1) = " " THEN V$ =  LEFT$ (A$,X - 1):T$ =  RIGHT$ (A$, LEN (A$) - X): GOTO 170
160  NEXT X: IF V$ = "" THEN V$ = A$: GOTO 400
170  FOR X = 1 TO  LEN (T$)
180  IF  MID$ (T$,X,1) = " " THEN 200
190  NEXT X:N$ = N$ + T$: GOTO 400
200  IF  LEFT$ (T$,X - 1) = "THE" THEN T$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 170
210  IF  LEFT$ (T$,X - 1) = "AND" THEN FLAG = 1:A$ = V$ + " " +  RIGHT$ (T$, LEN (T$) - X): GOTO 400
220  IF  LEFT$ (T$,X - 1) = "THEN" THEN FLAG = 1:A$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 400
230  IF  LEFT$ (T$,X - 1) = "." THEN FLAG = 1:A$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 400
240  IF  LEFT$ (T$,X - 1) = "TO" THEN T$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 170
250  IF  LEFT$ (T$,X - 1) = "WITH" THEN DO$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 300
260  IF  LEFT$ (T$,X - 1) = "AT" THEN DO$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 300
270  IF  LEFT$ (T$,X - 1) = "IN" THEN DO$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 300
275  IF  LEFT$ (T$,X - 1) = "ONTO" THEN DO$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 300
277  IF  LEFT$ (T$,X - 1) = "INTO" THEN DO$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 300
278  IF  LEFT$ (T$,X - 1) = "ON" THEN DO$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 300
280 N$ = N$ +  LEFT$ (T$,X):T$ =  RIGHT$ (T$, LEN (T$) - X): GOTO 170
300  FOR X = 1 TO  LEN (DO$): IF  MID$ (DO$,X,1) = " " THEN  GOTO 320
310  NEXT X: GOTO 400
320  IF  LEFT$ (DO$,X - 1) = "THE" THEN DO$ =  RIGHT$ (DO$, LEN (DO$) - X): GOTO 300
400  IF  RIGHT$ (N$,1) = " " THEN N$ =  LEFT$ (N$, LEN (N$) - 1)
407  FOR X = 1 TO 15: IF MR(X) = RM AND MP(X) > 0 THEN MO = 1: ELSE  NEXT :MO = 0
410  IF V$ = "N" OR V$ = "NORTH" THEN 705
420  IF V$ = "S" OR V$ = "SOUTH" THEN  GOTO 720
430  IF V$ = "E" OR V$ = "EAST" THEN  GOTO 740
435  IF V$ = "W" OR V$ = "WEST" THEN  GOTO 760
437  IF V$ = "GO" OR V$ = "WALK" OR V$ = "TRAVEL" THEN V$ = N$: GOTO 410
440  IF V$ = "GET" OR V$ = "TAKE" THEN  GOTO 800
450  IF V$ = "DROP" THEN  GOTO 910
460  IF V$ = "OPEN" THEN  GOTO 1010
470  IF V$ = "ATTACK" THEN 1100
480  IF V$ = "THROW" OR V$ = "TOSS" THEN  GOTO 1210
490  IF V$ = "READ" THEN  GOTO 1310
500  IF V$ = "SEARCH" THEN  GOTO 1410
510  IF V$ = "INVENTORY" OR V$ = "I" THEN  GOTO 1510
520  IF V$ = "DIAGNOSE" THEN  GOTO 1610
530  IF V$ = "EXAMINE" THEN  GOTO 1710
540  IF V$ = "LOOK" OR V$ = "L" THEN  GOTO 1810
550  IF V$ = "LIGHT" THEN  GOTO 1910
560  IF V$ = "UP" OR V$ = "U" THEN  GOTO 2000
570  IF V$ = "DOWN" OR V$ = "D" THEN  GOTO 2100
580  IF V$ = "SAVE" THEN  GOTO 5000
699  PRINT "I DON'T KNOW HOW TO '";V$;"'": GOTO 100
700  REM  MOVEMENT: NORTH,SOUTH,EAST,WEST
705  IF MO = 1 THEN  PRINT "THE MONSTER BLOCKS YOUR PATH": GOTO 100
710  IF N(RM) > 0 THEN RM = N(RM): GOTO 3000: ELSE  PRINT "CAN'T GO THAT WAY": BELL : GOTO 100
720  IF MO = 1 THEN  PRINT "THE MONSTER BLOCKS YOUR PATH": GOTO 100
730  IF S(RM) > 0 THEN RM = S(RM): GOTO 3000: ELSE  PRINT "CAN'T GO THAT WAY": BELL : GOTO 100
740  IF MO = 1 THEN  PRINT "THE MONSTER BLOCKS YOUR PATH": GOTO 100
750  IF E(RM) > 0 THEN RM = E(RM): GOTO 3000: ELSE  PRINT "CAN'T GO THAT WAY": BELL : GOTO 100
760  IF MO = 1 THEN  PRINT "THE MONSTER BLOCKS YOUR PATH": GOTO 100
770  IF W(RM) > 0 THEN RM = W(RM): GOTO 3000: ELSE  PRINT "CAN'T GO THAT WAY": BELL : GOTO 100
800  REM  GET-TAKE
830  FOR X = 1 TO 13: IF N$ = IT$(X) AND IT(X) <  > 70 AND IT(X) = RM THEN IT(X) = 70: PRINT : PRINT N$;" TAKEN": GOTO 100: ELSE  NEXT : PRINT "I DON'T SEE A ";N$;" HERE"
840  GOTO 100
900  REM  DROP
910  FOR X = 1 TO 12: IF N$ = IT$(X) AND IT(X) = 70 THEN  PRINT N$;" DROPPED":IT(X) = RM: GOTO 100: ELSE  NEXT : PRINT "YOU DON'T HAVE A ";N$
920  GOTO 100
1000  REM  OPEN
1010  IF N$ = "DOOR" AND RM = 21 AND DO$ = "KEY" AND IT(12) = 70 THEN S(21) = 19: PRINT "YOU OPEN THE DOOR EASILY": GOTO 100
1011  IF N$ = "DOOR" AND RM = 21 AND DO$ = "" THEN  PRINT "YOU MUST TELL ME WHAT TO OPEN THE DOOR  WITH": GOTO 100: ELSE  IF N$ = "DOOR" AND RM = 21 AND DO$ <  > "" AND DO$ <  > "KEY" THEN  PRINT "YOU CAN'T OPEN THE DOOR WITH THAT!!!!":G
1020  IF N$ = "CHEST" AND IT(10) = 70 THEN  PRINT "INSIDE THE CHEST IS A MIRAD OF GOLD     AND OTHER STUFF TOO VARIOUS TO MENTION":CH = 1: GOTO 100: ELSE  IF N$ = "CHEST" THEN  PRINT "YOU DON'T HAVE THE CHEST": GOTO 100
1030  PRINT "I SEE NO ";N$;" HERE": GOTO 100
1100  REM  ATTACK
1105  IF RM = 1 THEN  PRINT "THE PERSON AT THE BOX LOOKS AT YOU,     THEN PULLS OUT A BLACK SQUARE OF UNKNOWNMATERIAL AND A LOADSTONE AND PUTS THE   TWO TOGETHER...": END 
1106  IF DO$ = "" AND IT(3) = 70 THEN  PRINT "(WITH SWORD)": GOTO 1110: ELSE  PRINT "YOU DON'T HAVE THE SWORD": GOTO 100
1108  IF DO$ <  > "SWORD" THEN  PRINT "YOU CAN'T ATTACK WITH THAT": GOTO 100
1110  FOR X = 1 TO 15: IF MN$(X) = N$ AND MR(X) = RM THEN  GOTO 1130: ELSE  NEXT : PRINT "I SEE NO ";N$;" HERE": GOTO 100
1130  IF  INT ( RND (1) * 20) + 1 <  INT ((ST + DEX) / 2) + 3 THEN  PRINT "YOU HIT!!!!": BELL : GOTO 1140: ELSE  PRINT "YOU MISS!!!!": GOTO 100
1140 D =  INT ( RND (1) * 6) + 1:MP(X) = MP(X) - D: IF MP < 0 THEN MP = 0
1145  PRINT "AND YOU DO ";D;" POINTS OF": PRINT "DAMAGE": IF MP(X) <  = 0 THEN  PRINT "AND YOU KILL IT !!!": BELL : BELL : GOTO 100: ELSE  PRINT "BUT HE'S STILL ALIVE": GOTO 100
1200  REM  THROW OR TOSS
1210  IF N$ = "TORCH" AND RM = 36 AND FLAG(1) = 1 THEN  PRINT "THE TORCH IGNITES THE WEB AND THUSLY    THE SPIDER. HOWEVER, THE WEB GIVES WAY  AND YOU FALL...": GOTO 2500
1215  IF N$ = "TORCH" THEN  PRINT "OKAY, YOU THROW THE TORCH WHICH FALLS   THROUGH THE WEB.":IT(4) = 37
1220  IF RM = 45 AND IT(13) = 70 AND N$ = "BLACK FLASK" OR N$ = "FLASK" THEN  PRINT "THE FLASK HITS THE KING AND SHATTERS    SPREADING THE POISON ON HIM AND KILLING HIM":MP(12) = 0: GOTO 100
1230  FOR X = 1 TO 15: IF IT$(X) = N$ AND IT(X) = 70 THEN 1240: ELSE  PRINT "YOU DON'T HAVE THE ";N$: GOTO 100
1240  PRINT "OK... ";N$;":THROWN":IT(X) = RM: GOTO 100
1300  REM  READ
1310  IF N$ = "SCROLL" AND IT(1) = 70 AND RM <  > 65 THEN  PRINT "THE SCROLL SAYS 'READ ME WHEN SHADOWS   THREATEN": GOTO 100
1320  IF N$ = "SCROLL" AND IT(1) = 70 AND RM = 65 AND MP(15) > 0 THEN  PRINT "THE SHADOW LORD FALLS TO HIS KNEES AND  SCREAMS 'NO!!! NOT THE SPELL OF DISMISS-AL!!' AND AS YOU FINISH, BOTH HE AND    THE SCROLL VANISH":IT(1) = 0:MR(15) = 0: GOTO 100
1330  PRINT "YOU CAN'T READ THAT, PERHAPS EXAMINE IT FIRST": GOTO 100
1400  REM  SEARCH
1410  IF N$ <  > "ROOM" AND N$ <  > "PASSAGE" AND N$ <  > "CAVE" THEN  PRINT "SORRY, YOU CAN SEARCH ROOMS ONLY": GOTO 100
1420  IF RM = 60 THEN  PRINT "YOU FIND A PASSAGE THAT LEADS NORTH!!!":N(60) = 56: GOTO 100
1430  IF RM = 33 THEN  PRINT "DIGGING THROUGH THE ASH, YOU FIND A     SECRET PASSAGE TO THE EAST":E(33) = 34: GOTO 100
1440  PRINT "YOU SEARCH THE ";N$;" AND": PRINT "FIND NOTHING": GOTO 100
1500  REM  INVENTORY
1510  PRINT "YOU ARE CURRENTLY HOLDING:"
1515  FOR X = 1 TO 15: IF IT(X) = 70 THEN  PRINT "A ";IT$(X): NEXT : GOTO 100
1520  NEXT : GOTO 100
1600  REM  DIAGNOSE
1610  PRINT "YOU CURRENTLY HAVE ";HP;" HIT": PRINT "POINTS,DOWN FROM "; INT ((CO + ST) / 2);" POINTS"
1620  GOTO 100
1700  REM  EXAMINE
1710  IF N$ = "SCROLL" AND IT(1) = 70 THEN  PRINT "TRY READING THE SCROLL": GOTO 100: ELSE  IF N$ = "SCROLL" THEN  PRINT "YOU DON'T HAVE THAT"
1720  IF N$ = "MATCHES" AND IT(5) = 70 THEN  PRINT "THE MATCHBOOK COVER SAYS 'WHY NOT TRY A VACATION IN N0RWAY THIS YEAR??": GOTO 100: ELSE  IF N$ = "MATCHES" THEN  PRINT "YOU DON'T HAVE THEM": GOTO 100
1730  IF N$ = "BOTTLE" AND IT(6) = 70 THEN  PRINT "THE BOTTLE READ 'GRANDMA'S FAITHFUL HIT POINT RESTORER, WARNING DON'T...": GOTO 100: ELSE  IF N$ = "BOTTLE" THEN  PRINT "YOU DON'T HAVE THAT": GOTO 100
1740  IF N$ = "GOBLET" AND IT(7) = 70 THEN  PRINT "THE GOBLET HAS VERY FINE WORKMANSHIP, INLAYED WITH VARIOUS PRECIOUS JEWELS": GOTO 100: ELSE  IF N$ = "GOBLET" THEN  PRINT "YOU DON'T HAVE THAT": GOTO 100
1750  IF N$ = "SCEPTER" AND IT(8) = 70 THEN  PRINT "THE SCEPTER HAS VERY FINE WORKMANSHIP, INLAYED WITH VARIOUS PRECIOUS JEWELS": GOTO 100: ELSE  IF N$ = "SCEPTER" THEN  PRINT "YOU DON'T HAVE THAT": GOTO 100
1760  IF N$ = "CROWN" AND IT(7) = 70 THEN  PRINT "THE CROWN HAS VERY FINE WORKMANSHIP, INLAYED WITH VARIOUS PRECIOUS JEWELS": GOTO 100: ELSE  IF N$ = "CROWN" THEN  PRINT "YOU DON'T HAVE THAT": GOTO 100
1770  IF N$ = "FLASK" AND IT(13) = 70 THEN  PRINT "THEN FLASK READS, 'GRAMDMA'S OLDE RAT   POISON. USE ON THE KING SIZED RAT ONLY": GOTO 100: ELSE  IF N$ = "FLASK" THEN  PRINT "YOU DON'T HAVE THAT"
1780  FOR X = 1 TO 15: IF IT$(X) = N$ THEN 1790: ELSE  PRINT "YOU DON'T HAVE THE ";N$: GOTO 100
1790  PRINT "I SEE NOTHING REMARKABLE ABOUT THE ": PRINT N$
1795  GOTO 100
1800  REM  LOOK
1810  GOTO 3000
1900  REM  LIGHT
1910  IF N$ <  > "LANTERN" AND N$ <  > "TORCH" AND N$ <  > "MATCHES" THEN  PRINT "YOU CAN'T LIGHT THE ";N$: GOTO 100
1920  IF N$ = "MATCHES" AND IT(5) = 70 THEN  PRINT "LIGHTING THE MATCHES IS USELESS, YOU    SHOULD USE THEM TO LIGHT THINGS INSTEAD": GOTO 100: ELSE  IF N$ = "MATCHES" THEN  PRINT "YOU DON'T HAVE THEM": GOTO 100
1925  IF DO$ = "" THEN  PRINT "YOU MUST TELL ME WHAT TO LIGHT THAT     WITH": GOTO 100
1930  IF DO$ <  > "MATCHES" THEN  PRINT "YOU CAN'T USE THAT TO LIGHT THE ";N$: GOTO 100
1935  IF IT(5) <  > 70 THEN  PRINT "YOU DON'T HAVE THE MATCHES": GOTO 100
1940  IF N$ = "TORCH" AND IT(4) = 70 THEN  PRINT "THE TORCH IS NOW LIT":FLAG(1) = 1: GOTO 100: ELSE  IF N$ = "TORCH" THEN  PRINT "YOU DON'T HAVE THAT"
1950  IF N$ = "LANTERN" AND IT(2) = 70 THEN  PRINT "THE LANTERN IS NOW LIT":FLAG(2) = 1: GOTO 100: ELSE  IF N$ = "LANTERN" THEN  PRINT "YOU DON'T HAVE THE LANTERN": GOTO 100
1960  GOTO 100
2000  REM  UP
2010  IF RM = 19 THEN  PRINT "YOU ASCEND THE STAIRS":RM = 58: GOTO 3000
2020  IF RM = 37 THEN  PRINT "THERE IS NO WAY UP THE SMOOTH SIDE OF   THE WALL": GOTO 100
2030  IF RM = 4 THEN  PRINT "YOU GO UP THE STEPS...":RM = 1: GOTO 3000
2040  IF RM = 8 THEN  PRINT "YOU GO UP THE STEPS...":RM = 3: GOTO 3000
2050  IF RM = 52 THEN  PRINT "YOU HAVE SUPRISINGLY GOOD LEVERAGE AND  ARE ABLE TO PUSH THE GRATE UP AND GET   OUT":FLAG(5) = 1
2060  IF RM = 27 THEN  PRINT "YOU GO UP THE LONG SPIRAL STAIRCASE AND FINALLY...":RM = 26: GOTO 3000
2070  PRINT "UNLESS YOU ARE A BIRD, YOU CAN'T GET    THERE FROM HERE": GOTO 100
2100  REM  DOWN
2110  IF RM = 58 THEN  PRINT "YOU DESCEND TO GROUND LEVEL":RM = 19: GOTO 3000
2120  IF RM = 1 THEN  PRINT "YOU GO DOWN THE TOWER'S STEPS UNTIL...":RM = 4: GOTO 3000
2130  IF RM = 3 THEN  PRINT "YOU TRIP AND FALL DOWN THE STAIRS ON    YOUR FACE UNTIL...":RM = 8: GOTO 3000
2140  IF RM = 26 THEN  PRINT "YOU MAKE THE LONG AND TEDIOUS JOURNEY   DOWN INTO THE DARK DEPTHS...":RM = 27: GOTO 3000
2150  IF RM = 24 AND FLAG(5) = 0 THEN  PRINT "YOU CAN'T SEEM TO OPEN THE GRATE FROM   THIS SIDE": GOTO 100
2160  IF RM = 24 AND FLAG(5) = 1 THEN  PRINT "OKAY, YOU SLIP DOWN INTO THE MUCK AGAIN":RM = 52: GOTO 3000
2170  PRINT "YOU CAN'T GO THAT WAY": GOTO 100
2300  REM  MONSTER ATTACKS
2310  IF Z = 15 AND MC(Z) = 0 THEN  PRINT "THE SHADOW LORD JUMPS OFF THE THRONE    AND SAYS'HA, MORTAL! I SHALT GIVE THEE  THE FIRST BLOW'":MC(15) = 1: RETURN 
2320  IF Z = 13 AND MC(13) = 0 THEN  PRINT "SUDDENLY, BELOW YOU THE OOZE BUBBLES    AND A TENTICAL REACHES FOR YOU":MC(13) = 1
2330  IF Z = 4 AND MC(4) = 0 THEN  PRINT "A LARGE SNAKE APPROCHES AND SAYS, 'AND  NOW FOR SOMETHING COMPLETELY DIFFERENT":MC(4) = 1
2340  IF Z = 8 AND MC(8) = 0 THEN  PRINT "SUDDENLY A SUIT OF ARMOR JUMPS OFF A    PEDISTAL AND DRAWS IT'S SWORD":MC(8) = 1
2350  IF Z = 3 AND MC(3) = 0 THEN  PRINT "TO ADD INSULT TO INJURY,ALL THIS COMMO- TION SEEMS TO HAVE ROUSED A CREATURE    FROM THE CORNER":MC(3) = 1
2360  IF Z = 9 AND MC(9) = 0 THEN  PRINT "THE SPIDER THAT OWNS THIS WEB BEGINS TO MOVE TOWARDS YOU!!!":MC(9) = 1
2370  IF Z = 2 AND MC(2) = 0 THEN  PRINT "SUDDENLY A PIECE OF MACHINERY DETACHES  ITSELF FROM THE WALL AND ATTACKS YOU!!":MC(2) = 1
2375  IF MP(Z) <  = 0 THEN  PRINT "THERE IS A BODY OF A ";MN$(Z);" HERE": RETURN 
2380  PRINT "A ";MN$(Z);" ATTACKS YOU!!!": IF  INT ( RND (1) * 20) + 1 > MH(Z) THEN  PRINT "THE ATTACK MISSES": RETURN 
2390 D =  INT ( RND (1) * MD(Z)) + 1: PRINT "THE ATTACK DOES ";D;" POINTS OF DAMAGE":HP = HP - D: IF HP <  = 0 THEN  PRINT "YOU DIE": BELL : BELL : BELL : GOTO 2700
2395  RETURN 
2490  REM  BOTTOM OF THE PIT
2500  PRINT "YOU FALL WITH A THUD! BUT SEEM NOT TO   HURT YOURSELF. ":RM = 37: GOTO 3000
2700  REM  YOU DIE!!!
2710  PRINT "YOU DIED": END 
3000  REM  PRINT ROOMS
3010  GOSUB (RM * 10) + 3050
3020  FOR X = 1 TO 15: IF IT(X) = RM THEN  PRINT "THERE IS A ";IT$(X);" HERE ": PRINT 
3030  NEXT 
3040  GOTO 100
3060  PRINT "IN THIS WELL-LIT ROOM SIT A PERSON AT A STRANGLY CONSTRUCTED CONTRAPTION,TWO    BOXES, STACKED ON TOP IF ONE ANOTHER.   THE TOPMOST BOX HAS STRANGE GLOWING "
3063  PRINT "SYMBOLS ON IT'S FRONT. THE BOTTOM ONE,  WHICH EXTENDS AT AN ANGLE DOWN FROM THE OTHER HAS AN ARRAY OF BUTTONS ON IT.    THE PERSON WORKING THIS MACHINE IS      MUTTERING STRANGE INCANTAIONS LIKE"
3064  PRINT "'*$%@*!!! OVERFLOW ERRORS!!!!'. THE      PERSON SEEMS TOTALLY ENGROSSED IN THIS ANCIENT RITUAL AND TOTALLY IGNORES YOU. THE ONLY EXIT IS DOWN"
3065  RETURN 
3070  PRINT "YOU ARE OUTSIDE THE GATE WHICH IS TO    THE NORTH. THE DOORWAY, WITH THE PORT-  CULLIS UP, REMINDS YOU OMINOUSLY OF A   GIANT MOUTH": RETURN 
3080  PRINT "THIS OLD ROOM APPARENTLY USE TO BE A    WIZARD'S LABRATORY. OLD FLASKS AND      SHEETS OF VELLIUM ARE SCATTERED ABOUT,  ALL EITHER BROKEN OR OF NO DECERNABLE   USE. SUPRISINGLY, ONE AREA IS BLOCKED"
3085  PRINT "OFF BY SAND BAGS (PERHAPS SOME VERY     HURRIED RESERCH WAS CONDUCTED BEFORE    THE INVASION...). THE ONLY EXIT IS DOWN"
3087  RETURN 
3090  PRINT "YOU ARE AT WHAT SEEMS TO BE THE BASE OF A TOWER. STAIRS LEAD UP AND THE CORRIDORGOES EAST AND BENDS NORTH."
3095  RETURN 
3100  PRINT "YOU ARE IN A DIMLY LIT EAST-WEST        CORRIDOR. THERE IS A GREAT AMOUT OF     DEBRIS AND OLD, RUSTED WEAPONS. BUT YOU FIND NO EVIDENCE OF ANIMAL HABITATION..."
3103  RETURN 
3110  IF FLAG(5) = 1 AND FLAG(3) = 1 AND FLAG(4) = 1 AND MP(15) = 0 THEN  PRINT "YOU FEEL IN YOUR MIND A PRESENCE THAT   SCANS YOU, AND FINALLY YOU FEEL A       MESSAGE, MUCH THE SAME AS WHEN YOU      ENTERED.": GOTO 50000
3113  IF FLAG(6) = 1 THEN  PRINT "YOU ARE IN THE ENTRY AREA OF THE CASTLE. THE PORTCULLIS IS CLOSED. THERE IS A   LARGE ROOM TO THE NORTH, AND PASSAGES   LEADS EAST AND WEST.": RETURN 
3115 FLAG(6) = 1: PRINT "AS YOU ENTER THE CASTLE, THE PORCULLIS  SLAMS DOWN BEHIND YOU. YOU FEEL IN YOUR MIND A VOICE SO LOUD THAT YOU ARE       BROUGHT TO YOUR KNEES. THE VOICE SAYS"
3117  PRINT "'HA, MORTAL, THOUGH HAST DELIVERED      THINESELF INTO MY HANDS. THOU SHALT     NEVER LEAVE THIS PLACE ALIVE": RETURN 
3120  PRINT "YOU ARE IN AN EAST-WEST CORRIDOR. THERE IS ALSO A DOOR TO THE NORTH": RETURN 
3130  PRINT "YOU ARE AT A CURVE IN THE CORRIDOR. A   STAIRWAY LEADS UP AND THE CORRIDOR GOES NORTH AND WEST": RETURN 
3140  PRINT "YOU ARE IN A NORTH-SOUTH CORRIDOR. THEREIS ALSO A DOOR TO THE EAST. THE PASSAGE SEEMS TO GET DARKER FURTHER ON": RETURN 
3150  PRINT "APPARENTLY THIS ROOM WAS SOME KIND OF   BARRACKS WITH RUINED FURNITURE AND      PERSONAL EFFECTS STREWN ALL OVER THE    PLACE. THERE ARE DOORS TO THE EAST AND  THE WEST": RETURN 
3160  PRINT "THIS LARGE ROOM WAS APPRENTLY THE MAIN  HALL OF THE CASTLE. MURALS AND OTHER    WORKS OF ART HAVE FADED WITH THE YEARS  AND AN INTRICATE STAIN-GLASS DOME ABOVE HAS BEEN SHATTERED, LEAVING SHARDS "
3165  PRINT "TO MAKE A SPARKLING ABSTRACT PATTERN    ON THE MARBLE FLOOR. THE ROOM STRECHES  NORTH AND TO THE SOUTH ARE TWO LARGE    UNHINGED DOORS. ALSO THERE IS A DOOR TO THE WEST": RETURN 
3170  PRINT "THESE APPEAR TO HAVE BEEN A DORM-TYPE   ROOM WITH SEVERAL BEDS AND SMASHED FURN-ITURE. COB WEBS ARE PREVALENT IN THE    CORNERS OF THE ROOM. THERE ARE EXITS    NORTH AND SOUTH": RETURN 
3180  PRINT "YOU ARE IN A NORTH-SOUTH CORRIDOR.      THERE IS A DOOR OFF TO THE EAST": RETURN 
3190  PRINT "YOU HAVE ENTERED A GREAT LIBRARY.       VOLUMES OF BOOKS LINE EACH WALL. SEVERALOPEN BOOKS SHOW INCOMPREHENSIBLE RUINS  AND DIAGRAMS. THE ONLY EXIT IS TO THE   WEST": RETURN 
3200  PRINT "YOU ARE AT THE BACK OF THE GRAND HALL.  HERE THE ROOF HAS COLLAPSED, APARENTLY  DURING THE FINAL BATTLE AS THERE ARE    SKELETONS OF HUMANS AND OTHER THINGS    UNDER THE DEBRIS."
3205  PRINT "THERE IS A DOOR TO THE EAST AND THE     GREAT HALL STRETCHES SOUTH": RETURN 
3210  PRINT "YOU ARE IN APPARENTLY THE KITCHEN.      THERE ARE ROTTED FOOD STUFFS, POTS PANS AND OTHER UTENSIL SCATTERED ABOUT.DOORS LEAD SOUTH AND WEST": RETURN 
3220  PRINT "YOU ARE IN A NORTH-SOUTH CORRIDOR. TO   THE EAST IS A DOOR. THE CORRIDOR SEEMS  TO LIGHTEN FURTHER ON NORTH": RETURN 
3230  PRINT "THIS ROOM IS THE ARMORY. RUSTED WEAPONS AND ARMOR LINE THE WALLS ALL APPARENTLY UNSERVICEABLE. THE ONLY EXIT IS WEST": RETURN 
3240  PRINT "YOU ARE IN THE STAIRWELL. STAIRS        (WHAT WERE YOU EXPECTING, AN ELEVaTOR ?) LEAD UP. THE ONLY OTHER EXIT IS NORTH": RETURN 
3250  PRINT "YOU ARE AT A BEND IN THE CORRIDOR.LIGHT COMES THOUGH AN ARCHWAY LEADING TO THE  COURTYARD. THE HALLWAY GOES SOUTH AND   EAST": RETURN 
3260  PRINT "YOU ARE IN AN EAST-WEST CORRIDOR. AN    IMPRESSIVE DOOR IS TO THE NORTH AND ONE TO THE SOUTH WHICH IS ";
3263  IF S(23) = 19 THEN  PRINT "UNLOCKED": RETURN 
3264  PRINT "LOCKED": RETURN 
3270  PRINT "YOU ARE IN AN EAST-WEST CORRIDOR. THERE IS AN OPENING LEADING TO A LARGE ROOM   NORTH.": RETURN 
3280  PRINT "YOU ARE AT A BEND IN THE CORRIDOR.      THE CORRIDOR NOW GOES WEST AND SOUTH.": RETURN 
3290  PRINT "THIS OPEN AREA ONCE MAY HAVE BEEN AN    IMPRESSIVE COURTYARD. AS IT IS MANY OF  THE COBBLESTONES ARE MISSING AND TREES  WITHERED IF NOT DEAD. MORE SIGNS OF     BATTLE ARE PREVALENT IN SKELETONS AND"
3295  PRINT "BROKEN EQUIPTMENT. THERE IS A DRAIN IN  THE CENTER OF THE COURTYARD. THE ONLY   EXIT IS SOUTH": RETURN 
3300  PRINT "YOU ARE IN A CHAPEL. MANY OF THE SACRED CARVINGS AND STATUES HAVE BEEN DEFACED  AND MUCH OF WHAT WAS INTRICATE WOODWORK HAS ROTTED AWAY": RETURN 
3310  PRINT "YOU ARE IN THE KNIGHT'S HALL. TABLES ANDCHAIRS ARE SMASHED AND MORE EVIDENCE OF BATTLE IS EVIDENT. THERE IS A STAIRWAY  DOWN AND AN OPENING SOUTH": RETURN 
3320  IF FLAG(2) <  > 1 THEN  PRINT "IT IS VERY DARK HERE, IF YOU DON'T GET  SOME LIGHT THEN YOU MIGHT TRIP AND BREAKYOUR NECK": RETURN 
3325  PRINT "YOU ARE AT THE BOTTOM OF THE STAIRWELL. PASSEGES LEAD SOUTH AND EAST": RETURN 
3330  IF FLAG(2) <  > 1 THEN  PRINT "YOU TRIP AND BREAK YOUR NECK": GOTO 2700
3335  PRINT "YOU ARE AT A GUARD STATION. OLD WEAPONS ARE ON RACKS HERE. THE DUST IS PRACTI-  CALLY KNEE DEEP HERE PASSAGES LEAD EAST AND WEST": RETURN 
3340  PRINT "YOU ARE IN A DANK PASSAGE LEADING EAST  AND WEST. A CELL IS OFF TO THE SOUTH": RETURN 
3350  PRINT "YOU HAVE APPARENTLY FOUND THE TORCHURE  CHAMBER OF THE DUGEON. MANY ROTTED      PIECES OF EQUPTMENT LIE ABOUT": RETURN 
3360  IF FLAG(2) <  > 1 THEN  PRINT "YOU TRIP AND BREAK YOUR NECK": GOTO 2700
3365  PRINT "YOU HAVE ENTERED A ROOM PILED HIGH WITH COAL AND SHOVELS. A LARGE HOLE THAT IS  THE FURNACE IS TO THE SOUTH": RETURN 
3370  PRINT "THIS CELL HAS SEVERAL CORPSES STILL     ATTACHED TO THE WALL BY CHAINS. THE ONLYEXIT IS NORTH": RETURN 
3380  PRINT "YOU ARE INSIDE THE FURNACE. THE CHIMNEY STRETCHES UP INTO BLACKNESS. THE ONLT   EXIT IS NORTH": RETURN 
3390  PRINT "YOU ARE IN A LARGE NATURAL CAVERN.      STALAGMITES AND STALAGTITES LINE A PATH TO THE EAST. A HOLE IS WEST": RETURN 
3400  PRINT "YOU ARE IN A GLASS-SMOOTH PASSAGE.      WHILE YOU WALK YOU SLIP AND FALL,SLIDINGINTO A WEB": FOR X = 100 TO 1 STEP  - 1: TONE X,10: NEXT :RM = 36
3410  PRINT "YOU ARE STUCK IN A WEB. YOU HAVE A SMALLAMOUNT OF MOBILITY IN YOUR ARMS BUT THATIS IT": RETURN 
3420  PRINT "YOU ARE IN THE BOTTOM OF A PIT. THERE ISNO VISIBLE WAY UP. THE ONLY EXIT IS EAST": RETURN 
3430  PRINT "YOU ARE IN A DIMLY LIT E/W CORRIDOR.    A MUSKY SMELL SEEMS TO COME FROM THE    EAST": RETURN 
3440  PRINT "YOU ARE IN A CAVE. THERE ARE RAT DROPP- INGS ALL OVER THE FLOOR. PASSAGES LEAD  NORTH,SOUTH AND WEST": RETURN 
3450  PRINT "YOU ARE IN A RAT WARREN. EVIDENCE OF    RAT'S HABITATION ARE STREWN ALL OVER THECAVE": RETURN 
3460  PRINT "YOU AR IN A TWISTY PASSAGE LEADING SOUTHAND WEST": RETURN 
3470  PRINT "YOU ARE IN A TWISTY PASSAGE LEADING     SOUTH AND EAST": RETURN 
3480  PRINT "YOU ARE IN A TWISTY PASSAGE THAT LEADS  WEST AND SOUTH": RETURN 
3490  PRINT "IN THIS SMALL THERE IS A SMALL DESK HEREBESIDE IT A RACK OF SMALL WEAPONS.ONLY  EXIT IS SOUTH": RETURN 
3500  PRINT "THIS IS THE OPULENT KING'S ROOM.INLAYED WITH GOLD AND TAPESTRY. THE ONLY EXIT   IS NORTH": RETURN 
3510  PRINT "YOU ARE IN A TWISTY PASSAGE LEADING     NORTH AND EAST": RETURN 
3520  PRINT "YOU ARE IN A TWISTY PASSAGE LEADING     NORTH SOUTH EAST AND WEST": RETURN 
3530  PRINT "YOU ARE IN A DANK STINKING SEWER (AND   YOU THOUGHT ADVENTURING WAS ALL GLORY!!)PASSAGES LEAD SOUTH AND EAST": RETURN 
3540  PRINT "YOU ARE AT A BREACH IN THE WALL OF THE  SEWER AND THE RAT TUNNELS YOU MAY GO    EAST OR WEST": RETURN 
3550  PRINT "YOU ARE IN A TWISTY PASSAGE LEADING EASTAND WEST": RETURN 
3560  PRINT "YOU ARE IN A TWISTY PASSAGE LEADING     WEST AND NORTH": RETURN 
3570  PRINT "YOU ARE IN THE SEWER. THIS IS A DEAD ENDTHERE IS A GRATE ABOVE YOU": RETURN 
3580  PRINT "YOU ARE AT A BEND IN THE CORRIDOR. IT   LEADS EAST AND SOUTH": RETURN 
3590  PRINT "YOU ARE IN AN EAST-WEST PASSAGE. THERE  IS A DOOR ALSO TO YOUR SOUTH": RETURN 
3600  PRINT "YOU ARE AT A BEND IN THE CORRIDOR. THE  PASSAGES GO WEST AND SOUTH": RETURN 
3610  PRINT "THIS IS THE GREAT TRASURE ROOM. IT'S    ONCE GREAT STORES HAVE BEEN SOMEWHAT    DEPLETED.": RETURN 
3620  PRINT "YOU ARE IN A NORTH-SOUTH PASSAGE": RETURN 
3630  PRINT "YOU ARE AT THE TOP OF THE STAIRS. THERE IS A DOOR TO YOUR NORTH": RETURN 
3640  PRINT "YOU ARE IN A NORTH-SOUTH CORRIDOR. THEREARE IMPRESSIVLY ORNATE DOORS TO THE EAST": RETURN 
3650  PRINT "YOU ARE IN THE KING'S BEDROOM. THE FOUR-POSTER FEATHER BED IS STILL IN GOOD     CONDITION.THE ONLY EXIT IS WEST": RETURN 
3660  PRINT "THIS IS A SITTING ROOM OVERLOOKING THE  COUNTRYSIDE. AT ONE TIME THE SCENE MIGHTHAVE BEEN BREATHTAKING, BUT NOW IT'S A  PANORAMA OF DISESE AND BLIGHT.ONLY EXIT"
3665  PRINT "IS EAST": RETURN 
3670  PRINT "YOU ARE AT A BEND IN THE CORRIDOR. IT   LEADS EAST AND NORTH. THERE IS A DOOR TO .THE WEST": RETURN 
3680  PRINT "YOU ARE IN AN EAST-WEST CORRIDOR.TO THE NORTH IS AN IMPRESSIVE SET OF DOUBLE    DOORS.": RETURN 
3690  PRINT "YOU ARE AT A BEND IN THE CORRIDOR. IT   LEADS NORTH AND WEST.": RETURN 
3700  PRINT "YOU ARE IN THE MAIN THRONE ROOM. THIS   ROOM STILL HOLDS ALL THE ADORNMENTS OF  IT'S FORMER SELF. AS APPARENTLY THIS IS THE THRONE ROOM OF THE SHADOW LORD.": RETURN 
5000  PRINT  CHR$ (4);"OPEN SAVEGAME"
5010  PRINT  CHR$ (4);"WRITE SAVEGAME"
5020  PRINT ST: PRINT CO: PRINT DEX: PRINT HP: PRINT RM
5025  FOR X = 1 TO 15: PRINT MP(X): PRINT MR(X): NEXT : FOR X = 1 TO 6: PRINT FLAG(X): NEXT : FOR X = 1 TO 13: PRINT IT(X): NEXT 
5030  PRINT  CHR$ (4);"CLOSE SAVEGAME"
50000  REM  YOU WIN!!!!
55000  DATA   0,0,0,0,6,0,0,0,0,0,0,0,9,0,5,0,0,0,6,4,11,2,7,5,12,0,8,6,13,0,0,7,17,4,10,0,0,0,11,9,15,6,0,10    
55010  DATA    16,7,0,0,23,8,14,0,0,0,0,13,0,11,16,0,0,12,0,15,20,9,18,0,0,0,0,17,21,0,0,0,24,17,21,0,25,0 ,22,20,26,0,23,21  
55020  DATA   0,13,0,22,0,20,0,0,0,21,0,0,0,22,0,0,0,31,28,0,0,0,29,27,0,32,30,28,0,0,0,29,27,33,0,0,29,0,0,0,31,0,34,0,0,0,35,33,0,0,0,0,0,0,0,0,0,0,38,0 
55030  DATA  0,0,39,37,41,40,0,0,39,0,0,0,0,39,0,47,0,45,43,0,0,46,0,42,0,47,0,0,42,0,0,0,43,0,47,0,44,51,41,46,0,52,49,0
55040  DATA   0,0,50,48,0,0,51,49,47,0,0,50,48,0,0,0,0,57,54,0,0,58,53,55,0,59,0,54,0,60,0,0,53,62,0,0,54,0,0,0,55,64,60,0,0,0,0,59,0,0,62,0,57,0,63,61,65,0,64,62,59,0,0,63,0,63,0,0  
55050  DATA   "SCROLL",14,"LANTERN",2,"SWORD",2,"TORCH",27,"MATCHES",2,"BOTTLE",3,"GOBLET",61,"SCEPTOR",65,"DIAMONDS",25, "RING",60,"CROWN",65,"KEY",3,"FLASK"
55055  DATA  16
55060  DATA    "RABBIT",9,2 ,34,10,"IRON MAIDEN",6,3,30,7,"PIT THING",8,2,37,13,"MONTY PYTHON"    ,6, 4,11,15,"SKELETON",4,1,4,2,"SHADOW FIEND",2,7,25,4 
55065  DATA   "ROTTING CORPSE",4,2,32,3,"EMPTY ARMOR",5,3,18,6,"SPIDER",2,3,36,32,"RAT WORKER",3,2,40,3,"RAT GUARD",9,4,44,7  
55066  DATA  "RAT KING"
55070  DATA   7,5 ,45,11,SLIME,4,3,48,10,GHOUL,12,2,61,12,SHADOW LORD,19,17,65,25
59999  END 
60000  HOME : PRINT "FOR MANY YEARS THE EVIL CASTLE HAS      LOOMED OVER YOUR VILLAGE, DESERTED      SINCE THE WARS WITH THE DEAMONS. SOME   SAY THAT DEAMONS LIVE THERE NOW BUT     THAT IS UNKNOWN. WHAT IS KNOW IS THAT";
60005  PRINT "": PRINT "THE TREASURE OF THE OLD KINGS LIES  "
60010  PRINT "WITHIN. SOME BRASH AND FOOLISH YOUTHS   OF THE VILLAGE HAVE TRIED TO GAIN THIS  TREASURE, ALL HAVE FAILED AND MOST NEVERRETURNED. THOSE THAT DID WOULD HAVE     BEEN BETTER DEAD. ARE YOU ANY DIFFERENT THAN THOSE THAT WENT BEFORE ?"
60020  PRINT : PRINT "     YOU'D BETTER BE...."
60025  VTAB 20
60030  PRINT "DO YOU NEED INSTRUCTIONS (Y/N)?";: GET A$
60040  IF A$ = "Y" OR A$ = "N" THEN  GOTO 60050: ELSE  PRINT "": PRINT  CHR$ (7);"INCORRECT ENTRY PLEASE BE SURE THE CAPS LOCK KEY IS DOWN AND ANSWER AGAIN": GOTO 60025
60050  IF A$ = "N" THEN  GOTO 60500
60060  HOME : PRINT  TAB( 14);"INSTRUCTIONS": FOR X = 1 TO 40: PRINT "-";: NEXT 
60070  PRINT " IN THIS GAME YOU PLAY A CHARACTER THAT IS CREATED FOR YOU. YOUR OBJECT IS TO   GATHER AS MUCH TREASURE AS POSSIBLE. TO DO THIS YOU WILL HAVE TO OVERCOME MANY"
60080  PRINT "TRAPS, MONSTERS, AND OTHER HAZARDS.     USUALLY THERE IS ONLY ONE WAY TO SOLVE  EACH PROBLEM. YOUR CHARACTER IS MADE UP OF THREE NUMBERS OR STATISTICS. EACH OF THESE ARE DETERMINED RANDOMLY AND"
60090  PRINT "ARE USED MAINLY TO FIGURE YOUR COMBAT   CHANCES (CHANCES TO HIT, BE HIT ETC.)   AFTER YOU HAVE CREATED YOUR CHARACTER   YOU WILL START YOUR ADVENTURE."
60100  PRINT : PRINT "PRESS ANY KEY TO CONTINUE....";: GET A$
60110  GOSUB 60200
60120  GOSUB 60300
60130  GOTO 20
60200  HOME 
60205  PRINT  TAB( 11);"CHARACTER CREATION": FOR OX = 1 TO 40: PRINT "-";: NEXT 
60210 ST =  INT ( RND (1) * 20) + 1: IF ST < 7 THEN  GOTO 60210
60220 CO =  INT ( RND (1) * 20) + 1: IF CO < 7 THEN  GOTO 60220
60230 DEX =  INT ( RND (1) * 20) + 1: IF DEX < 7 THEN  GOTO 60230
60235 HP =  INT (CO + ST) / 2
60240  PRINT "THIS IS YOUR CHARACTER:": PRINT : PRINT "STRENGTH    :";ST: PRINT : PRINT "DEXTERITY   :";DEX: PRINT : PRINT "CONSTITUTION:";CO
60250  PRINT : PRINT "20 IS THE HIGHEST POSSIBLE. ABOUT 9 OR  10 IS AVERAGE. ": PRINT : PRINT "YOU MAY K)EEP THIS CHARACTER OR M)AKE   A NEW ONE. PLEASE ENTER YOUR COMMAND    (K OR M)";
60260  GET A$: IF A$ <  > "K" AND A$ <  > "M" THEN  PRINT "": PRINT "PLEASE ANSWER K OR M ONLY": BELL : GOTO 60250
60270  IF A$ = "M" THEN 60200
60280  RETURN 
60300  HOME 
60303  HTAB 10: PRINT "STARTING THE ADVENTURE"
60306  PRINT "----------------------------------------";
60309  HTAB 2: PRINT "NOW THAT YOU HAVE YOUR CHARACTER, IT IS";
60312  PRINT "TIME TO START YOUR ADVENTURE. THE COMPUT";
60315  PRINT "ER WILL EXPECT YOU TO GIVE IT ORDERS IN"
60318  PRINT "A FULL-SENTENCE FORMAT.  ALTHOUGH YOU"
60321  PRINT "CAN ENTER ANYTHING, ONLY A FEW KEY WORDS";
60324  PRINT "WILL BE RECOGNIZED. A LIST OF THESE"
60327  PRINT "WORDS WILL FOLLOW. SEVERAL EXAMPLES OF"
60330  PRINT "HOW TO ENTER TEXT ARE:"
60333  HTAB 2: PRINT "1 TO GET MORE THAN ONE THING, USE 'AND'";
60336  PRINT "I.E. 'GET THE ROCK AND THE CANDY' THIS"
60339  PRINT "IS THE ONLY ALLOWABLE USE OF 'AND'"
60342  HTAB 2: PRINT "2 USE 'WITH' TO SPECIFY WHAT YOU ARE"
60345  PRINT "USING TO COMPLETE A TASK EXAMPLE"
60348  PRINT "'ATTACK THE COMPUTER WITH THE HATCHET'"
60351  HTAB 2: PRINT "3 USE 'THEN' TO MAKE MORE THAN ONE MOVE";
60354  PRINT "AT A TIME I.E. 'GO WEST THEN EAST'"
60357  INVERSE : PRINT "WARNING!!!";: NORMAL : PRINT " USE ONLY STATMENTS THAT"
60360  PRINT "REQUIRE ";: INVERSE : PRINT "NO DIRECT OBJECT";: NORMAL : PRINT " SUCH AS 'GET'"
60363  PRINT "AND 'GO'. IT IS MUCH EASIER JUST TO DO"
60366  PRINT "THINGS ONE AT A TIME, FOR YOU AND THE"
60369  PRINT "COMPUTER. PRESS ANY KEY TO CONTINUE";: GET A$
60372  HOME : PRINT "  4 OTHER PREPOSITIONS THAT ARE ALLOWED"
60375  PRINT "ARE: 'IN' 'AT' 'ONTO' 'ON'"
60378  HTAB 3: PRINT "5 NOTE THAT 'THE' IS TOTALLY IGNORED"
60381  PRINT "SO THAT YOU MAY USE IT AS MUCH AS YOU"
60384  PRINT "LIKE SUCH AS 'GET THE THE THE CAR'"
60387  HTAB 13: PRINT "-----------------"
60390  HTAB 3: PRINT "A LIST OF THE WORDS, AND EXPLAINATIONS";
60393  PRINT "THAT THIS PROGRAM WILL EXCEPT ARE AS"
60396  PRINT "FOLLOWS:"
60399  PRINT "NORTH,SOUTH,EAST,WEST WILL MOVE YOU IN"
60402  PRINT "THAT DIRECTION. THESE MAYBE COMMANDS IN"
60405  PRINT "THEMSELVES, JUST TYPE IN 'NORTH'. ALSO"
60408  PRINT "THESE ARE ABBREVIATED TO N,S,E,W"
60411  VTAB 14: HTAB 2: PRINT "GET OR TAKE"
60414  VTAB 16: HTAB 2: PRINT "DROP- DROPS AN ITEM I.E. 'DROP THE GUN'";
60417  VTAB 18: HTAB 2: PRINT "OPEN, THROW OR TOSS, READ, ATTACK AND     LIGHT"
60420  VTAB 20: HTAB 2: PRINT "SEARCH- LOOK FOR HIDDEN THINGS IN A"
60423  HTAB 2: PRINT "ROOM"
60426  PRINT "   PRESS ANY KEY TO CONTINUE";: GET A$
60429  HOME 
60432  HTAB 2: PRINT "DIAGNOSE- THIS WILL GIVE YOU A REPORT"
60435  PRINT "OF YOUR CURRENT PYSICAL CONDITION"
60438  VTAB 4: HTAB 2: PRINT "EXAMINE- THIS WILL HAVE YOU STUDY AN"
60441  PRINT "OBJECT FOR ANYTHING UNUSUAL"
60444  VTAB 7: HTAB 2: PRINT "LOOK OR L- REPRINTS THE ROOM DESCRIP-"
60447  PRINT "TION"
60450  VTAB 10: HTAB 2: PRINT "INVENTORY OR I- GIVES YOU A LIST OF"
60453  PRINT "EVERYTHING YOU ARE CARRYING"
60456  VTAB 13: HTAB 2: PRINT "IF ANY ACTION THAT YOU WANT TO TRY IS"
60459  PRINT "NOT SHOWN ABOVE, IT PROBABLY WON'T WORK"
60462  VTAB 16: HTAB 2: PRINT "SAVE- SAVES THE GAME AT THE POSITION"
60465  PRINT "YOU ARE IN AND STOPS THE GAME. WHEN YOU"
60468  PRINT "RUN THE PROGRAM AGAIN, SELECT THE"
60471  PRINT "OPTION TO RESTART A GAME TO PICK UP"
60474  PRINT "WHERE YOU LEFT OFF."
60477  VTAB 22: HTAB 8: INVERSE : PRINT "PRESS ANY KEY TO CONTINUE";: NORMAL 
60480  GET A$
60490  RETURN 
60500  HOME : PRINT  TAB( 13);"MAIN SELECTION": FOR X = 1 TO 40: PRINT "-";: NEXT 
60510  PRINT : PRINT "    1) CREATE NEW CHARACTER"
60520  PRINT : PRINT "    2) RESTART OLD ADVENTURE"
60525  PRINT : PRINT "    3) QUIT"
60530  VTAB 15: INPUT "PLEASE ENTER YOUR SELECTION (1,2,OR 3)";A
60540  IF A < 0 OR A > 3 THEN  BELL : PRINT "PLEASE ANSWER 1, 2, OR 3 ONLY": GOTO 60530
60550  IF A = 1 THEN  GOSUB 60200: GOTO 20
60560  IF A = 3 THEN  PRINT "ARE YOU SURE (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN  GOTO 60560: ELSE  IF A$ = "Y" THEN  END : ELSE  GOTO 60500
60570  PRINT "": PRINT  CHR$ (4);"OPEN SAVEGAME"
60580  PRINT  CHR$ (4);"READ SAVEGAME"
60585  INPUT ST: INPUT CO: INPUT DEX: INPUT HP: INPUT RM
60590  FOR X = 1 TO 15: INPUT MP(X): INPUT MR(X): NEXT : FOR X = 1 TO 6: INPUT FLAG(X): NEXT : FOR X = 1 TO 13: INPUT IT(X): NEXT 
60600  PRINT  CHR$ (4);"CLOSE SAVEGAME"
60610  PRINT  CHR$ (4);"DELETE SAVEGAME
60620  GOTO 3000

]
